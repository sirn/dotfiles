{ config, lib, pkgs, ... }:

{
  programs.wezterm = {
    enable = config.machine.isNixOS || pkgs.stdenv.isDarwin;

    colorSchemes = {
      foot = {
        ansi = [
          "#242424"
          "#f62b5a"
          "#47b413"
          "#e3c401"
          "#24acd4"
          "#f2affd"
          "#13c299"
          "#e6e6e6"
        ];
        brights = [
          "#616161"
          "#ff4d51"
          "#35d450"
          "#e9e836"
          "#5dc5f8"
          "#feabf2"
          "#24dfc4"
          "#ffffff"
        ];

        background = "#242424";
        cursor_bg = "#ffffff";
        cursor_border = "#ffffff";
        cursor_fg = "#242424";
        foreground = "#ffffff";
        scrollbar_thumb = "#d1d1d1";
        selection_bg = "#d1d1d1";
        selection_fg = "#242424";
      };
    };

    extraConfig = ''
      local config = wezterm.config_builder()
      local shell = "${config.programs.fish.package}/bin/fish"

      config.color_scheme = 'foot'
      config.enable_scroll_bar = true
      config.font = wezterm.font_with_fallback({
        'PragmataPro Mono',
        'Source Han Code JP',
      })

      config.command_palette_font = wezterm.font_with_fallback({
        'PragmataPro Mono',
        'Source Han Code JP',
      })

      config.freetype_load_target = 'Light'
      config.freetype_load_flags = 'NO_HINTING'
      config.hide_tab_bar_if_only_one_tab = true
      config.prefer_egl = true
      config.use_ime = true
      config.mux_enable_ssh_agent = false

      config.window_padding = {
        left = '0.2cell',
        right = '1cell',
        top = '0',
        bottom = '0',
      }

      config.initial_cols = 120
      config.initial_rows = 36

      ${lib.optionalString pkgs.stdenv.isLinux ''
        config.default_prog = { shell };
        config.font_size = 12.0
        config.front_end = "WebGpu"
        config.enable_wayland = true
      ''}

      ${lib.optionalString pkgs.stdenv.isDarwin ''
        config.default_prog = { shell, "--login" }
        config.font_size = 14.0;
      ''}

      return config
    '';
  };

  # Hack until we can use null package in Home Manager 25.04
  xdg.configFile =
    let
      cfg = config.programs.wezterm;

      tomlFormat = pkgs.formats.toml { };

      shellIntegrationStr = ''
        source "/etc/profile.d/wezterm.sh"
      '';
    in
    (lib.mkIf (!config.machine.isNixOS && !pkgs.stdenv.isDarwin) (lib.mkMerge [
      {
        "wezterm/wezterm.lua".text = ''
            -- Generated by Home Manager.
          -- See https://wezfurlong.org/wezterm/

          local wezterm = require 'wezterm'

          ${cfg.extraConfig}
        '';
      }
      (lib.mapAttrs'
        (name: value:
          lib.nameValuePair "wezterm/colors/${name}.toml" {
            source = tomlFormat.generate "${name}.toml" { colors = value; };
          })
        cfg.colorSchemes)
    ]));
}
